plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'dev.hoodieboi.creativeworld'
version = '1.1.0'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven { url = "https://repo.onarandombox.com/content/groups/public/" }
    maven { url 'https://libraries.minecraft.net/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.3-R0.1-SNAPSHOT'
    compileOnly 'com.onarandombox.multiversecore:Multiverse-Core:4.3.1'
    compileOnly 'net.luckperms:api:5.4'
    implementation 'me.lucko:commodore:2.2'
    implementation "net.kyori:adventure-text-serializer-legacy:4.12.0"
}

shadowJar {
    dependsOn build

    dependencies {
        exclude(dependency('com.mojang:brigadier'))
        exclude(dependency('net.kyori:.*'))
    }

    relocate 'me.lucko.commodore', 'dev.hoodieboi.creativeworld.commodore'
    classifier = null
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
